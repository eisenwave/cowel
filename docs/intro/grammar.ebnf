document
  = { document-markup-element };
document-markup-element
  = document-text
  | common-markup-element;
document-text
  = document-text-character, { document-text-character };
document-text-character
  = ? Any character ? - "\";

common-markup-element
  = escape
  | comment
  | directive;
escape
  = "\", escapable
  | "\", line-terminator;
escapable
  =  "{" |  "}" |  "[" |  "]" |  "(" |  ")" |  "," |  "." |  "=" |  "!"
  |  '"' |  "#" |  "$" |  "%" |  "&" |  "'" |  "*" |  "+" |  "/" |  ";"
  |  "<" |  ">" |  "?" |  "@" |  "^" |  "|" |  "~" |  "-"
  |  " " |  TAB |   VT ;
comment
  = "\:", ? Maximal character sequence up to and including line-terminator ?;

directive
  = "\", directive-name, [ group ], [ block ];
directive-name
  = directive-name-character - digit, { directive-name-character };
directive-name-character
  = "_" | digit | ? ASCII alphabetic character ?;
digit
  = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9";

group
  = "(", blank, [ group-member-list ], blank, ")";
group-member-list
  = group-member,
    { blank, ",", blank, group-member },
    blank, [ "," ];
group-member
  = member-name, blank, "=", blank, member-value
  | member-value
  | "...";
member-name
  = member-name-character, { member-name-character };
member-name-character
  = ? HTML attribute name ? - ("\" | "(" | ")" | "{", "}" | ",");
member-value
  = directive-call
  | primary-value;
directive-call
  = directive-name, blank, group, [ blank, block ]
  | directive-name, blank, block;
primary-value
  = unit-literal
  | null-literal
  | boolean-literal
  | integer-literal
  | unquoted-string
  | quoted-string
  | group
  | block;
unit-literal
  = "unit";
null-literal
  = "null";
boolean-literal
  = "true" | "false";
integer-literal
  =  [ "-" ], digit, { digit };
unquoted-string
  = unquoted-string-character, { unquoted-string-character };
unquoted-string-character
  = directive-name-character | "-";

quoted-string
  = '"', { quoted-string-markup-element }, '"';
quoted-string-markup-element
  = quoted-string-text
  | common-markup-element;
quoted-string-text
  = quoted-string-text-character, { quoted-string-text-character };
quoted-string-text-character
  = ? Any character ? - ("\" | '"');

block
  = "{", { block-markup-element }, "}";
block-markup-element
  = block-text
  | common-markup-element;
block-text
  = block-text-character, { block-text-character } (* brace-balanced *);
block-text-character
  = ? Any character ? - "\";

blank
  = { comment | whitespace };
whitespace
  = { " " | TAB | LF | CR | VT };
line-terminator
  = LF
  | CR, LF
  | CR (* unless the next character is LF *);
TAB = ? U+0009 CHARACTER TABULATION ? (* \t in C *);
LF =  ? U+000A END OF LINE ?          (* \n in C *);
CR =  ? U+000D CARRIAGE RETURN ?      (* \r in C *);
VT =  ? U+000B LINE TABULATION ?      (* \v in C *);
