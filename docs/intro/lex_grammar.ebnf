DOCUMENT
  = { DOCUMENT-MARKUP-ELEMENT };
DOCUMENT-MARKUP-ELEMENT
  = DOCUMENT-TEXT-TOKEN
  | COMMON-MARKUP-ELEMENT;
DOCUMENT-TEXT-TOKEN
  = DOCUMENT-TEXT-CHAR, { DOCUMENT-TEXT-CHAR };
DOCUMENT-TEXT-CHAR
  = CHAR - "\";

COMMON-MARKUP-ELEMENT
  = ESCAPE-TOKEN
  | LINE-COMMENT-TOKEN
  | BLOCK-COMMENT-TOKEN
  | DIRECTIVE-SPLICE
  | RESERVED-ESCAPE-TOKEN;
ESCAPE-TOKEN
  = "\", ESCAPABLE-CHAR
  | "\", LINE-TERMINATOR;
ESCAPABLE-CHAR
  = "{" |  "}" | '"' | "\";
RESERVED-ESCAPE-TOKEN
  = "\", RESERVED-ESCAPE-CHAR
  | "\", ? end of file ?;
RESERVED-ESCAPE-CHAR
  = CHAR
  - ESCAPABLE-CHAR
  - IDENTIFIER-START-CHAR
  - "*" - ":" - LF - CR;
LINE-COMMENT-TOKEN
  = "\:", { LINE-COMMENT-CHAR }, LINE-TERMINATOR;
LINE-COMMENT-CHAR
  = CHAR - (LF | CR);
BLOCK-COMMENT-TOKEN
  = "\*", { BLOCK-COMMENT-CHAR }, "*\";
BLOCK-COMMENT-CHAR
  = CHAR - ("*" ? followed by "\" ?);
DIRECTIVE-SPLICE
  = DIRECTIVE-SPLICE-NAME-TOKEN, GROUP, BLOCK
  | DIRECTIVE-SPLICE-NAME-TOKEN, BLOCK
  | DIRECTIVE-SPLICE-NAME-TOKEN, GROUP ? not followed by "{" ?
  | DIRECTIVE-SPLICE-NAME-TOKEN ? not followed by "(" or "{" ?;
DIRECTIVE-SPLICE-NAME-TOKEN
  = "\", IDENTIFIER-TOKEN;

IDENTIFIER-TOKEN
  = IDENTIFIER-START-CHAR, { IDENTIFIER-CHAR }
  ? not followed by IDENTIFIER-CHAR ?;
IDENTIFIER-START-CHAR
  = "_" | LETTER;
IDENTIFIER-CHAR
  = IDENTIFIER-START-CHAR | DIGIT;
DIGIT
  = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9";
LETTER
  = ? ASCII alphabetic character ?;

GROUP
  = PARENTHESIS-LEFT-TOKEN, { GROUP-CONTENT }, PARENTHESIS-RIGHT-TOKEN;
PARENTHESIS-LEFT-TOKEN
  = "(";
PARENTHESIS-RIGHT-TOKEN
  = ")";
GROUP-CONTENT
  = GROUP
  | BLOCK
  | IDENTIFIER-TOKEN
  | QUOTED-STRING
  | BINARY-INT-TOKEN
  | OCTAL-INT-TOKEN
  | DECIMAL-INT-TOKEN
  | HEXADECIMAL-INT-TOKEN
  | DECIMAL-FLOAT-TOKEN
  | INFINITY-TOKEN
  | NEGATIVE-INFINITY-TOKEN
  | EQUALS-TOKEN
  | COMMA-TOKEN
  | ELLIPSIS-TOKEN
  | UNIT-TOKEN
  | NULL-TOKEN
  | TRUE-TOKEN
  | FALSE-TOKEN
  | LINE-COMMENT-TOKEN
  | BLOCK-COMMENT-TOKEN
  | WHITESPACE-TOKEN
  | RESERVED-NUMBER-TOKEN;
EQUALS-TOKEN
  = "=";
COMMA-TOKEN
  = ",";
ELLIPSIS-TOKEN
  = "...";
UNIT-TOKEN
  = "unit" ? not followed by IDENTIFIER-CHAR ?;
NULL-TOKEN
  = "null" ? not followed by IDENTIFIER-CHAR ?;
TRUE-TOKEN
  = "true" ? not followed by IDENTIFIER-CHAR ?;
FALSE-TOKEN
  = "false" ? not followed by IDENTIFIER-CHAR ?;

BINARY-INT-TOKEN
  = [ "-" ], "0b", BINARY-DIGIT, { BINARY-DIGIT };
OCTAL-INT-TOKEN
  = [ "-" ], "0o", OCTAL-DIGIT, { OCTAL-DIGIT };
DECIMAL-INT-TOKEN
  = [ "-" ], DIGIT, { DIGIT };
HEXADECIMAL-INT-TOKEN
  = [ "-" ], "0x", HEXADECIMAL-DIGIT, { HEXADECIMAL-DIGIT };
BINARY-DIGIT
  = "0" | "1";
OCTAL-DIGIT
  = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7";
HEXADECIMAL-DIGIT
  = DIGIT
  | "A" | "B" | "C" | "D" | "E" | "F"
  | "a" | "b" | "c" | "d" | "e" | "f";

INFINITY-TOKEN
  = "infinity" ? not followed by IDENTIFIER-CHAR ?;
NEGATIVE-INFINITY-TOKEN
  = "-infinity;" ? not followed by IDENTIFIER-CHAR ?;
DECIMAL-FLOAT-TOKEN
  = DECIMAL-FLOAT ? not followed by IDENTIFIER-CHAR ?;
DECIMAL-FLOAT
  = [ "-" ], DIGIT, { DIGIT }, ".", { DIGIT }, [ EXPONENT ]
  | [ "-" ], ".", DIGIT, { DIGIT }, [ EXPONENT ]
  | [ "-" ], DIGIT, { DIGIT }, EXPONENT;
EXPONENT
  = ("e" | "E"), [ "+" | "-" ], DIGIT, { DIGIT };

RESERVED-NUMBER-TOKEN
  = PERMISSIVE-NUMBER
  - BINARY-INT-TOKEN
  - OCTAL-INT-TOKEN
  - DECIMAL-INT-TOKEN
  - HEXADECIMAL-INT-TOKEN
  - DECIMAL-FLOAT-TOKEN;
PERMISSIVE-NUMBER
  = ( [ "-" ], DIGIT | "." ), ? maximal sequence ([0-9a-zA-Z.]|e+|E+|e-|E-)+ ?;

QUOTED-STRING
  = STRING-QUOTE-TOKEN, { QUOTED-STRING-MARKUP-ELEMENT }, STRING-QUOTE-TOKEN;
STRING-QUOTE-TOKEN
  = '"';
QUOTED-STRING-MARKUP-ELEMENT
  = QUOTED-STRING-TEXT-TOKEN
  | COMMON-MARKUP-ELEMENT;
QUOTED-STRING-TEXT-TOKEN
  = QUOTED-STRING-TEXT-CHAR, { QUOTED-STRING-TEXT-CHAR };
QUOTED-STRING-TEXT-CHAR
  = CHAR - ("\" | '"');

BLOCK
  = BRACE-LEFT-TOKEN, { BLOCK-MARKUP-ELEMENT }, BRACE-RIGHT-TOKEN;
BRACE-LEFT-TOKEN
  = "{";
BRACE-RIGHT-TOKEN
  = "}";
BLOCK-MARKUP-ELEMENT
  = BLOCK-TEXT-TOKEN
  | COMMON-MARKUP-ELEMENT;
BLOCK-TEXT-TOKEN
  = BLOCK-TEXT-ELEMENT, { BLOCK-TEXT-ELEMENT };
BLOCK-TEXT-ELEMENT
  = BLOCK-TEXT-CHAR
  | "{", { BLOCK-TEXT-ELEMENT }, "}";
BLOCK-TEXT-CHAR
  = CHAR - ("\" | "}");

WHITESPACE-TOKEN
  = { HT | LF | CR | VT | SP };
LINE-TERMINATOR
  = LF
  | CR, LF
  | CR ? not followed by LF ?;
HT = ? U+0009 CHARACTER TABULATION ?;
LF = ? U+000A END OF LINE ?;
CR = ? U+000D CARRIAGE RETURN ?;
VT = ? U+000B LINE TABULATION ?;
SP = ? U+0020 SPACE ?;
CHAR = ? any Unicode code point ?;
