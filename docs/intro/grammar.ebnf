document
  = { document-markup-element };
document-markup-element
  = DOCUMENT-TEXT-TOKEN
  | common-markup-element;
common-markup-element
  = ESCAPE-TOKEN
  | LINE-COMMENT-TOKEN
  | BLOCK-COMMENT-TOKEN
  | directive-splice;
directive-splice
  = DIRECTIVE-SPLICE-NAME-TOKEN, [ group ], [ block ];
identifier
  = IDENTIFIER-TOKEN;
group
  = PARENTHESIS-LEFT-TOKEN, [ group-member-list ], PARENTHESIS-RIGHT-TOKEN;
group-member-list
  = group-member, { COMMA-TOKEN, group-member }, [ COMMA-TOKEN ];
group-member
  = member-name, EQUALS-TOKEN, expression
  | expression
  | ELLIPSIS-TOKEN;
member-name
  = identifier
  | quoted-string;
expression
  = prefix-expression;
prefix-expression
  = postfix-expression
  | prefix-operator, prefix-expression;
prefix-operator
  = BITWISE-NOT-TOKEN
  | LOGICAL-NOT-TOKEN
  | PLUS-TOKEN
  | MINUS-TOKEN;
postfix-expression
  = primary-expression
  | directive-call-expression;
directive-call-expression
  = identifier, group, [ block ]
  | identifier, block;
primary-expression
  = UNIT-TOKEN
  | NULL-TOKEN
  | bool-literal
  | int-literal
  | float-literal
  | unquoted-string
  | quoted-string
  | block
  | group;
bool-literal
  = TRUE-TOKEN
  | FALSE-TOKEN;
int-literal
  = BINARY-INT-TOKEN
  | OCTAL-INT-TOKEN
  | DECIMAL-INT-TOKEN
  | HEXADECIMAL-INT-TOKEN;
float-literal
  = INFINITY-TOKEN
  | DECIMAL-FLOAT-TOKEN;
unquoted-string
  = IDENTIFIER-TOKEN ? not followed by group or block ?;
quoted-string
  = STRING-QUOTE-TOKEN, { quoted-string-markup-element }, STRING-QUOTE-TOKEN;
quoted-string-markup-element
  = QUOTED-STRING-TEXT-TOKEN
  | common-markup-element;
block
  = BRACE-LEFT-TOKEN, { block-markup-element }, BRACE-RIGHT-TOKEN;
block-markup-element
  = BLOCK-TEXT-TOKEN
  | common-markup-element;
