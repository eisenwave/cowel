document
  = { document-markup-element };
document-markup-element
  = DOCUMENT-TEXT-TOKEN
  | common-markup-element;
common-markup-element
  = ESCAPE-TOKEN
  | LINE-COMMENT-TOKEN
  | BLOCK-COMMENT-TOKEN
  | directive-splice;
directive-splice
  = DIRECTIVE-SPLICE-NAME-TOKEN, [ group ], [ block ];
identifier
  = unquoted-identifier
  | quoted-identifier;
unquoted-identifier
  = UNQUOTED-IDENTIFIER-TOKEN;
quoted-identifier
  = QUOTED-IDENTIFIER-TOKEN;
group
  = PARENTHESIS-LEFT-TOKEN, [ group-member-list ], PARENTHESIS-RIGHT-TOKEN;
group-member-list
  = group-member, { COMMA-TOKEN, group-member }, [ COMMA-TOKEN ];
group-member
  = identifier, EQUALS-TOKEN, member-value
  | member-value
  | ELLIPSIS-TOKEN;
member-value
  = directive-call
  | primary-value;
directive-call
  = unquoted-identifier, group, [ block ]
  | unquoted-identifier, block;
primary-value
  = UNIT-TOKEN
  | NULL-TOKEN
  | bool-literal
  | int-literal
  | float-literal
  | unquoted-string
  | quoted-string
  | block
  | group;
bool-literal
  = TRUE-TOKEN | FALSE-TOKEN;
int-literal
  = BINARY-INT-TOKEN
  | OCTAL-INT-TOKEN
  | DECIMAL-INT-TOKEN
  | HEXADECIMAL-INT-TOKEN;
float-literal
  = INFINITY-TOKEN
  | NEGATIVE-INFINITY-TOKEN
  | DECIMAL-FLOAT-TOKEN;
unquoted-string
  = UNQUOTED-IDENTIFIER-TOKEN;
quoted-string
  = STRING-QUOTE-TOKEN, { quoted-string-markup-element }, STRING-QUOTE-TOKEN;
quoted-string-markup-element
  = QUOTED-STRING-TEXT-TOKEN
  | common-markup-element;
block
  = BRACE-LEFT-TOKEN, { block-markup-element }, BRACE-RIGHT-TOKEN;
block-markup-element
  = BLOCK-TEXT-TOKEN
  | common-markup-element;
