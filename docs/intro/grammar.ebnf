document
  = { document-markup-element };
document-markup-element
  = document-text
  | common-markup-element;
document-text
  = document-text-char, { document-text-char };
document-text-char
  = ? any character ? - "\";

common-markup-element
  = escape
  | line-comment
  | block-comment
  | directive-splice;
escape
  = "\", escapable-char
  | "\", line-terminator;
escapable-char
  =  "{" |  "}" |  "[" |  "]" |  "(" |  ")" |  "," |  "." |  "="
  |  "!" |  '"' |  "#" |  "$" |  "%" |  "&" |  "'" |  "+" |  "/"
  |  ";" |  "<" |  ">" |  "?" |  "@" |  "^" |  "|" |  "~" |  "-"
  |  " " |  TAB |   VT ;
line-comment
  = "\:", { line-comment-char }, line-terminator;
line-comment-char
  = ? any character ? - (LF | CR);
block-comment
  = "\*", { block-comment-char }, "*\";
block-comment-char
  = ? any character ? - ("*" (* followed by "\" *));

directive-splice
  = "\", directive-name, [ group ], [ block ];
directive-name
  = directive-name-char - digit, { directive-name-char };
directive-name-char
  = "_" | digit | ? ASCII alphabetic character ?;
digit
  = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9";

group
  = "(", blank, [ group-member-list ], blank, ")";
group-member-list
  = group-member,
    { blank, ",", blank, group-member },
    blank, [ "," ];
group-member
  = member-name, blank, "=", blank, member-value
  | member-value
  | "...";
member-name
  = member-name-char, { member-name-char };
member-name-char
  = ? HTML attribute name ? - ("\" | "(" | ")" | "{", "}" | ",");
member-value
  = directive-call
  | primary-value;
directive-call
  = directive-name, blank, group, [ blank, block ]
  | directive-name, blank, block;
primary-value
  = unit-literal
  | null-literal
  | bool-literal
  | int-literal
  | float-literal
  | unquoted-string
  | quoted-string
  | block
  | group;
unit-literal
  = "unit";
null-literal
  = "null";
bool-literal
  = "true" | "false";

int-literal
  = binary-int-literal
  | octal-int-literal
  | decimal-int-literal
  | hexadecimal-int-literal;
binary-int-literal
  = [ "-" ], "0b", binary-digit, { binary-digit };
octal-int-literal
  = [ "-" ], "0o", octal-digit, { octal-digit };
decimal-int-literal
  = [ "-" ], digit, { digit };
hexadecimal-int-literal
  = [ "-" ], "0x", hexadecimal-digit, { hexadecimal-digit };
binary-digit
  = "0" | "1";
octal-digit
  = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7";
hexadecimal-digit
  = digit
  | "A" | "B" | "C" | "D" | "E" | "F"
  | "a" | "b" | "c" | "d" | "e" | "f";

float-literal
  = [ "-" ], "infinity"
  | decimal-float-literal;
decimal-float-literal
  = [ "-" ], digit, { digit }, ".", { digit }, [ exponent ]
  | [ "-" ], ".", digit, { digit }, [ exponent ]
  | [ "-" ], digit, { digit }, exponent;
exponent
  = ("e" | "E"), [ "+" | "-" ], digit, { digit };
unquoted-string
  = unquoted-string-char, { unquoted-string-char };
unquoted-string-char
  = directive-name-char | "-";

quoted-string
  = '"', { quoted-string-markup-element }, '"';
quoted-string-markup-element
  = quoted-string-text
  | common-markup-element;
quoted-string-text
  = quoted-string-text-char, { quoted-string-text-char };
quoted-string-text-char
  = ? any character ? - ("\" | '"');

block
  = "{", { block-markup-element }, "}";
block-markup-element
  = block-text
  | common-markup-element;
block-text
  = block-text-char, { block-text-char } (* brace-balanced *);
block-text-char
  = ? any character ? - "\";

blank
  = { line-comment | block-comment | whitespace };
whitespace
  = { " " | TAB | LF | CR | VT };
line-terminator
  = LF
  | CR, LF
  | CR (* unless the next character is LF *);
TAB = ? U+0009 CHARACTER TABULATION ? (* \t in C *);
LF =  ? U+000A END OF LINE ?          (* \n in C *);
CR =  ? U+000D CARRIAGE RETURN ?      (* \r in C *);
VT =  ? U+000B LINE TABULATION ?      (* \v in C *);
