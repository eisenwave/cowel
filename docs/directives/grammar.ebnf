document
  = { markup-element };
markup-element
  = text
  | escape
  | comment
  | directive;
text
  = (* Maximal character sequence excluding "\\" *);
escape
  = "\\", escapable
  | "\\", line-terminator;
escapable
  =  "{" |  "}" |  "[" |  "]" |  "(" |  ")" |  "," |  "." |  "=" |  "!"
  | "\"" |  "#" |  "$" |  "%" |  "&" |  "'" |  "*" |  "+" |  "/" |  ";"
  |  "<" |  ">" |  "?" |  "@" |  "^" |  "|" |  "~" |  "-"
  | "\t" |  " " |  "\v";
comment
  = "\\:", (* Maximal character sequence excluding line-terminator *);
line-terminator
  = "\n" | "\r\n" | "\r";
directive
  = "\\", directive-name, [ group ], [ "{", { markup-element }, "}" ];
directive-name
  = (* Sequence matching /[_a-zA-Z0-9][_a-zA-Z0-9]+/ *);
group
  = "(", group-member-list, blank, ")";
group-member-list
  = member-comments, group-member,
    { blank, ",", member-comments, group-member },
    blank, [ "," ];
group-member
  = member-name, blank, "=", blank, member-value
  | member-value
  | "...";
member-name
  = (* HTML attribute name minus "\\", "(", ")", "{", "}", "," *);
member-value
  = { member-value-markup-element }
  | group;
member-value-markup-element
  = member-value-text
  | escape
  | comment
  | directive;
member-value-text
  = (* Maximal character sequence excluding "\\", ",", or balanced ")" *);
member-comments
  = { comment | blank };
blank
  = { " " | "\t" | "\r" | "\n" | "\v" };
